IMPLEMENTED;ORDER;Topic;OWL Construct;SHACL Construct;SHACL Construct TYPE;QUERY
Y;0;Class definition; owl:Class, rdfs:Class;sh:NodeShape, sh:targetClass;Logical Constraint  Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

CONSTRUCT {  
 	?shapeUrl a sh:NodeShape ;
 		sh:targetClass ?type .
} WHERE {
 	?type a  ?typeClassUrl . 
	VALUES ?typeClassUrl {owl:Class rdfs:Class} .
	
	FILTER (!isBlank(?type)) .
	BIND ( URI(CONCAT(STR(?type),'Shape')) AS ?shapeUrl) .
}"
Y;0;Minimum qualified cardinality; owl:Class, rdfs:Class, owl:minQualifiedCardinality,owl:onClass, owl:Restriction;sh:NodeShape, sh:property, sh:qualifiedMinCount, sh:qualifiedValueShape, sh:class;Shape-based  Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
	?shapeUrl a sh:NodeShape;
		sh:property ?embeddedNode .
	?embeddedNode sh:qualifiedMinCount ?minQualifiedCardinality;
		sh:qualifiedValueShape [ sh:class ?onClassRestriction  ] .
} WHERE {
	?type a  ?typeClassUrl .
	VALUES ?typeClassUrl { owl:Class rdfs:Class }
	?type rdfs:subClassOf ?owlPropertyRestriction .
	?owlPropertyRestriction a owl:Restriction .
	?owlPropertyRestriction owl:onProperty ?rangedProperty .
	?owlPropertyRestriction owl:minQualifiedCardinality ?minQualifiedCardinality . 
 	?owlPropertyRestriction owl:onClass ?onClassRestriction . 
	
	BIND ( URI(CONCAT(STR(?rangedProperty),'-EmbeddedRestriction')) AS ?embeddedNode)

 	FILTER (!isBlank(?type)) .
 	BIND ( URI(CONCAT(STR(?type),'Shape')) AS ?shapeUrl) .
}"
Y;0;Minimum qualified cardinality; owl:Class, rdfs:Class, owl:minQualifiedCardinality,owl:onClass, owl:Restriction;sh:NodeShape, sh:property, sh:qualifiedMinCount, sh:qualifiedMaxCount,sh:qualifiedValueShape, sh:class;Shape-based  Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
	?shapeUrl a sh:NodeShape;
		sh:property ?embeddedNode .
	?embeddedNode sh:qualifiedMinCount ?qualifiedCardinality;
		sh:qualifiedMaxCount ?qualifiedCardinality;
		sh:qualifiedValueShape [ sh:class ?onClassRestriction  ] .
} WHERE {
	?type a  ?typeClassUrl .
	VALUES ?typeClassUrl { owl:Class rdfs:Class }
	?type rdfs:subClassOf ?owlPropertyRestriction .
	?owlPropertyRestriction a owl:Restriction .
	?owlPropertyRestriction owl:onProperty ?rangedProperty .
	?owlPropertyRestriction owl:qualifiedCardinality ?qualifiedCardinality . 
 	?owlPropertyRestriction owl:onClass ?onClassRestriction . 
	
	BIND ( URI(CONCAT(STR(?rangedProperty),'-EmbeddedRestriction')) AS ?embeddedNode)

 	FILTER (!isBlank(?type)) .
 	BIND ( URI(CONCAT(STR(?type),'Shape')) AS ?shapeUrl) .
}"
Y;0;is Defined By; owl:Class, rdfs:Class, rdfs:isDefinedBy ;sh:NodeShape, rdfs:isDefinedBy;Non-Validating Property Shape Characteristics;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>

CONSTRUCT {  ?shapeUrl a sh:NodeShape;
			 	rdfs:isDefinedBy  ?shapeNodeDefinedBy .
} WHERE { 
	?type a ?typeClassUrl .
	VALUES ?typeClassUrl {owl:Class rdfs:Class}
	?type rdfs:isDefinedBy ?shapeNodeDefinedBy .
 	FILTER (!isBlank(?type)) .
 	BIND ( URI(CONCAT(STR(?type),'Shape')) AS ?shapeUrl) .
}"
Y;0;seeAlso annotation; owl:Class, rdfs:Class, rdfs:seeAlso ;sh:NodeShape, rdfs:seeAlso;Non-Validating Property Shape Characteristics;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>

CONSTRUCT {  ?shapeUrl a sh:NodeShape;
			 	rdfs:seeAlso  ?shapeNodeSeeAlso .
} WHERE { 
	?type a ?typeClassUrl .
	VALUES ?typeClassUrl {owl:Class rdfs:Class}
	?type rdfs:seeAlso ?shapeNodeSeeAlso .
 	FILTER (!isBlank(?type)) .
 	BIND ( URI(CONCAT(STR(?type),'Shape')) AS ?shapeUrl) .
}"
Y;0;Functional Property definition; owl:FunctionalProperty;sh:PropertyShape, sh:maxCount;Cardinality Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>

CONSTRUCT { ?shapeUrl a sh:PropertyShape;
				sh:maxCount 1 .
} WHERE { 
	?property a owl:FunctionalProperty .
	BIND ( URI(CONCAT(STR(?property),'-Shape')) AS ?shapeUrl) .
}"
Y;0;Intersection of classes; owl:intersectionOf, owl:Class, rdfs:Class;sh:NodeShape, sh:and ;Logical Constraint  Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

CONSTRUCT {  
 	?shapeUrl a sh:NodeShape ;
		sh:and ?intersectedTypesList . 
	?intersectedTypesListRest rdf:first ?intersectedTypesListHeadShape ;
		rdf:rest ?intersectedTypesListTailShape .
} WHERE {
 	?type a  ?typeClassUrl . 
	VALUES ?typeClassUrl {owl:Class rdfs:Class} 
	OPTIONAL { ?type owl:intersectionOf ?intersectedTypesList .
		?intersectedTypesList rdf:rest* ?intersectedTypesListRest .
		?intersectedTypesListRest rdf:first ?intersectedTypesListHead .
		?intersectedTypesListRest  rdf:rest ?intersectedTypesListTail .
		BIND ( IF ( ?intersectedTypesListHead != rdf:nil && !isBlank(?intersectedTypesListHead), URI(CONCAT(STR(?intersectedTypesListHead),'Shape')), ?intersectedTypesListHead ) AS ?intersectedTypesListHeadShape )
		BIND ( IF ( ?intersectedTypesListTail != rdf:nil && !isBlank(?intersectedTypesListTail), URI(CONCAT(STR(?intersectedTypesListTail),'Shape')), ?intersectedTypesListTail ) AS ?intersectedTypesListTailShape )
	}
	FILTER (!isBlank(?type)) .
	BIND ( URI(CONCAT(STR(?type),'Shape')) AS ?shapeUrl) .
}"
Y;0;Object Property definition; owl:ObjectProperty;sh:PropertyShape, sh:nodeKind, sh:BlankNodeOrIRI;Value Type Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

CONSTRUCT {
	?shapeUrl a sh:PropertyShape;
		sh:nodeKind sh:BlankNodeOrIRI .
} WHERE { 
	?property a ?propertyType .
	VALUES ?propertyType { owl:ObjectProperty }
	FILTER (!isBlank(?property)) .
	BIND ( URI(CONCAT(STR(?property),'-Shape')) AS ?shapeUrl) .
}"
Y;0;Comment annotation; owl:ObjectProperty, owl:DatatypeProperty, rdfs:Datatype, rdfs:comment;sh:PropertyShape, sh:description, rdfs:label;Non-Validating Property Shape Characteristics;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>

CONSTRUCT {  ?shapeUrl a sh:PropertyShape;
			 	sh:description  ?propertyComment;
			 	rdfs:label ?propertyComment .
} WHERE { 
	?property a ?propertyType .
	VALUES ?propertyType {owl:ObjectProperty owl:DatatypeProperty rdfs:Datatype } .
	?property rdfs:comment ?propertyComment .
	FILTER (!isBlank(?property)) .
	BIND ( URI(CONCAT(STR(?property),'-Shape')) AS ?shapeUrl) .
}"
Y;0;Union of classes; owl:ObjectProperty, rdfs:range, owl:unionOf;sh:PropertyShape, sh:class;Value Type Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

CONSTRUCT {
	?shapeUrl a sh:PropertyShape;
		sh:class ?typeInRange, ?typeInRangeUnited .
} WHERE { 
		?property a ?propertyType .
		VALUES ?propertyType { owl:ObjectProperty }
		OPTIONAL { ?property rdfs:range ?typeInRange . 
					FILTER (!isBlank(?typeInRange)) .}
		OPTIONAL { ?property rdfs:range ?typeInRangeBlank .
				?typeInRangeBlank owl:unionOf ?typeInRangeUnited .
				FILTER (!isBlank(?typeInRangeUnited)) .
		}
		FILTER (!isBlank(?property)) .
		BIND ( URI(CONCAT(STR(?property),'-Shape')) AS ?shapeUrl) .
}"
Y;0;Class definition;owl:Class, rdfs:Class;sh:nodeKind, sh:NodeShape, sh:IRI;Value Type Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>

CONSTRUCT {
	?shapeUrl a sh:NodeShape; 
 		sh:nodeKind sh:IRI .
} WHERE {
    ?type a ?typeClassUrl . 
    VALUES ?typeClassUrl {owl:Class rdfs:Class}
	FILTER (!isBlank(?type)) .
	BIND ( URI(CONCAT(STR(?type),'Shape')) AS ?shapeUrl) .
}"
Y;0;Cardinality;owl:Class, owl:Restriction, rdfs:Class, owl:cardinality, owl:onProperty;sh:NodeShape,sh:property,sh:PropertyShape, sh:maxCount, sh:minCount;Cardinality Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>

CONSTRUCT {
	?shapeUrl a sh:NodeShape;
 	sh:property ?embeddedRestriction .
 	?embeddedRestriction a sh:PropertyShape;
		sh:maxCount ?cardinality;
		sh:minCount ?cardinality .
} WHERE {
    ?type a ?typeClassUrl . 
    VALUES ?typeClassUrl {owl:Class rdfs:Class}
    ?type rdfs:subClassOf ?owlPropertyRestriction .
    ?owlPropertyRestriction a owl:Restriction .
	OPTIONAL { ?owlPropertyRestriction owl:onProperty ?rangedProperty .
		?owlPropertyRestriction owl:cardinality ?cardinality . 
		BIND ( URI(CONCAT(STR(?rangedProperty),'-EmbeddedRestriction')) AS ?embeddedRestriction)
	}
	FILTER (!isBlank(?type)) .
	BIND ( URI(CONCAT(STR(?type),'Shape')) AS ?shapeUrl) .
}"
Y;0;Maximum cardinality;owl:Class, owl:Restriction, rdfs:Class, owl:maxCardinality, owl:onProperty;sh:NodeShape,sh:property,sh:PropertyShape, sh:maxCount;Cardinality Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>

CONSTRUCT {
	?shapeUrl a sh:NodeShape;
 	sh:property ?embeddedRestriction .
 	?embeddedRestriction a sh:PropertyShape;
		sh:maxCount ?maxCardinality .
} WHERE {
    ?type a ?typeClassUrl . 
    VALUES ?typeClassUrl {owl:Class rdfs:Class}
    ?type rdfs:subClassOf ?owlPropertyRestriction .
    ?owlPropertyRestriction a owl:Restriction .
	OPTIONAL { ?owlPropertyRestriction owl:onProperty ?rangedProperty .
		?owlPropertyRestriction owl:maxCardinality ?maxCardinality . 
		BIND ( URI(CONCAT(STR(?rangedProperty),'-EmbeddedRestriction')) AS ?embeddedRestriction)
	}
	FILTER (!isBlank(?type)) .
	BIND ( URI(CONCAT(STR(?type),'Shape')) AS ?shapeUrl) .
}"
Y;0;Minimum cardinality;owl:Class, owl:Restriction, rdfs:Class, owl:minCardinality, owl:onProperty;sh:NodeShape,sh:property,sh:PropertyShape, sh:minCount;Cardinality Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>

CONSTRUCT {
	?shapeUrl a sh:NodeShape;
 	sh:property ?embeddedRestriction .
 	?embeddedRestriction a sh:PropertyShape;
		sh:minCount ?minCardinality .
} WHERE {
    ?type a ?typeClassUrl . 
    VALUES ?typeClassUrl {owl:Class rdfs:Class}
    ?type rdfs:subClassOf ?owlPropertyRestriction .
    ?owlPropertyRestriction a owl:Restriction .
	OPTIONAL { ?owlPropertyRestriction owl:onProperty ?rangedProperty .
		?owlPropertyRestriction owl:minCardinality ?minCardinality . 
		BIND ( URI(CONCAT(STR(?rangedProperty),'-EmbeddedRestriction')) AS ?embeddedRestriction)
	}
	FILTER (!isBlank(?type)) .
	BIND ( URI(CONCAT(STR(?type),'Shape')) AS ?shapeUrl) .
}"
Y;0;Universal restriction;owl:Class, rdfs:Class, owl:allValuesFrom, owl:Restriction;sh:NodeShape, sh:PropertyShape, sh:datatype, sh:class;Value Type Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>
CONSTRUCT { 
 	?shapeUrl a sh:NodeShape;
 		sh:property ?embeddedRestriction .
 	?embeddedRestriction a sh:PropertyShape;
    		?variableRange ?valuesInRange .
} WHERE {
	?type a  ?typeClassUrl .
	VALUES ?typeClassUrl {owl:Class rdfs:Class}
	OPTIONAL {?type rdfs:subClassOf ?owlPropertyRestriction .
		?owlPropertyRestriction a owl:Restriction .
		OPTIONAL{ ?owlPropertyRestriction owl:onProperty ?rangedProperty .
				?owlPropertyRestriction owl:allValuesFrom ?valuesInRange .	
				BIND ( IF ( STRSTARTS(str(?valuesInRange),'http://www.w3.org/2001/XMLSchema#'), sh:datatype, sh:class ) AS ?variableRange )
		}
		BIND ( URI(CONCAT(STR(?rangedProperty),'-EmbeddedRestriction')) AS ?embeddedRestriction)
	}
 	FILTER (!isBlank(?type)) .
 	BIND ( URI(CONCAT(STR(?type),'Shape')) AS ?shapeUrl) .
}"
Y;0;Complement of a class;owl:Class, rdfs:Class, owl:complementOf;sh:NodeShape,sh:not;Logical Constraint  Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

CONSTRUCT {  
 	?shapeUrl a sh:NodeShape ;
 		sh:not ?complementTypeShape ; 
} WHERE {
 	?type a  ?typeClassUrl . 
	VALUES ?typeClassUrl {owl:Class rdfs:Class} .
	OPTIONAL { ?type owl:complementOf ?complementType . 
		BIND ( URI(CONCAT(STR(?complementType),'Shape')) AS ?complementTypeShape) .
		FILTER (!isBlank(?complementType))
	}
	FILTER (!isBlank(?type)) .
	BIND ( URI(CONCAT(STR(?type),'Shape')) AS ?shapeUrl) .
}"
Y;0;Disjoint with a class;owl:Class, rdfs:Class, owl:disjointWith;sh:NodeShape, sh:disjoint ;Property Pair  Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>
CONSTRUCT { 
 	?shapeUrl a sh:NodeShape ; 
 		sh:disjoint ?disjointType . 
} WHERE {
 	?type a  ?typeClassUrl . 
	VALUES ?typeClassUrl {owl:Class rdfs:Class} .
 	?type owl:disjointWith ?disjointType .
 	FILTER (!isBlank(?type)) .
 	BIND ( URI(CONCAT(STR(?type),'Shape')) AS ?shapeUrl) .
}"
Y;0;Equivalent classes;owl:Class, rdfs:Class, owl:equivalentClass;sh:NodeShape, sh:equals;Property Pair  Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>

CONSTRUCT { 
 	?shapeUrl a sh:NodeShape ; 
 		sh:equals ?sameAsType . 
} WHERE {
 	?type a  ?typeClassUrl . 
	VALUES ?typeClassUrl {owl:Class rdfs:Class} .
 	?type owl:equivalentClass ?sameAsType .
 	BIND ( URI(CONCAT(STR(?type),'Shape')) AS ?shapeUrl) .
}"
Y;0;Specific  values of a class;owl:Class, rdfs:Class, owl:hasValue;sh:NodeShape, sh:PropertyShape, sh:hasValue;Other Constraint;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>

CONSTRUCT {  
 	?shapeUrl a sh:NodeShape;
 	sh:property ?embeddedRestriction .
 	?embeddedRestriction a sh:PropertyShape;
 		sh:hasValue ?hasValue .
} WHERE {
 	?type a ?typeClassUrl . 
	VALUES ?typeClassUrl {owl:Class rdfs:Class} .
	?type ?restrictionPredicate ?owlPropertyRestriction .
	VALUES ?restrictionPredicate { rdfs:subClassOf } .
	?owlPropertyRestriction owl:onProperty ?rangedProperty .
	?owlPropertyRestriction owl:hasValue ?hasValue .
	BIND ( URI(CONCAT(STR(?rangedProperty),'-EmbeddedRestriction')) AS ?embeddedRestriction)			
	FILTER (!isBlank(?type)) .
	BIND ( URI(CONCAT(STR(?type),'Shape')) AS ?shapeUrl) .
}"
Y;0;Maximum cardinality;owl:Class, rdfs:Class, owl:maxQualifiedCardinality,owl:onClass, owl:Restriction;sh:NodeShape, sh:property, sh:qualifiedMaxCount, sh:qualifiedValueShape, sh:class;Shape-based  Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
	?shapeUrl a sh:NodeShape;
		sh:property ?embeddedNode .
	?embeddedNode sh:qualifiedMaxCount ?maxQualifiedCardinality;
		sh:qualifiedValueShape [ sh:class ?onClassRestriction  ] .
} WHERE {
	?type a  ?typeClassUrl .
	VALUES ?typeClassUrl { owl:Class rdfs:Class }
	?type rdfs:subClassOf ?owlPropertyRestriction .
	?owlPropertyRestriction a owl:Restriction .
	?owlPropertyRestriction owl:onProperty ?rangedProperty .
	?owlPropertyRestriction owl:maxQualifiedCardinality ?maxQualifiedCardinality . 
 	?owlPropertyRestriction owl:onClass ?onClassRestriction . 
	
	BIND ( URI(CONCAT(STR(?rangedProperty),'-EmbeddedRestriction')) AS ?embeddedNode)

 	FILTER (!isBlank(?type)) .
 	BIND ( URI(CONCAT(STR(?type),'Shape')) AS ?shapeUrl) .
}"
Y;0;Enumerated class;owl:Class, rdfs:Class, owl:oneOf;sh:NodeShape, sh:in;Other Constraint;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

CONSTRUCT {  
 	?shapeUrl a sh:NodeShape ;
 		sh:in ?inclusionTypesList . 
 	?inclusionTypesListRest rdf:first ?inclusionTypesListHeadShape ;
 		rdf:rest ?inclusionTypesListTailShape .
} WHERE {
 	?type a  ?typeClassUrl . 
	VALUES ?typeClassUrl {owl:Class rdfs:Class} .
	OPTIONAL { ?type owl:oneOf ?inclusionTypesList .
		?inclusionTypesList rdf:rest* ?inclusionTypesListRest .
		?inclusionTypesListRest rdf:first ?inclusionTypesListHead .
		?inclusionTypesListRest  rdf:rest ?inclusionTypesListTail .
		BIND ( IF ( ?inclusionTypesListHead != rdf:nil && !isBlank(?inclusionTypesListHead), URI(CONCAT(STR(?inclusionTypesListHead),'Shape')), ?inclusionTypesListHead ) AS ?inclusionTypesListHeadShape ) 
		BIND ( IF ( ?inclusionTypesListTail != rdf:nil && !isBlank(?inclusionTypesListTail), URI(CONCAT(STR(?inclusionTypesListTail),'Shape')), ?inclusionTypesListTail ) AS ?inclusionTypesListTailShape )
	}
	FILTER (!isBlank(?type)) .
	BIND ( URI(CONCAT(STR(?type),'Shape')) AS ?shapeUrl) .
}"
Y;0;Property definition;owl:Class, rdfs:Class, owl:onProperty, owl:Restriction;sh:NodeShape, sh:PropertyShape, sh:property, sh:path;Embedded sh:PropertyShapes;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>

CONSTRUCT { 
 	?shapeUrl a sh:NodeShape;
 		sh:property ?embeddedRestriction .
 	?embeddedRestriction a sh:PropertyShape;
    		sh:path ?rangedProperty .
} WHERE {
	?type a  ?typeClassUrl .
	VALUES ?typeClassUrl {owl:Class rdfs:Class}
	?type rdfs:subClassOf ?owlPropertyRestriction .
	?owlPropertyRestriction a owl:Restriction .
	?owlPropertyRestriction owl:onProperty ?rangedProperty .
	BIND ( URI(CONCAT(STR(?rangedProperty),'-EmbeddedRestriction')) AS ?embeddedRestriction)
 	FILTER (!isBlank(?type)) .
 	BIND ( URI(CONCAT(STR(?type),'Shape')) AS ?shapeUrl) .
}"
Y;0;Union of classes;owl:Class, rdfs:Class, owl:unionOf;sh:NodeShape, sh:or;Logical Constraint  Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

CONSTRUCT { 
	?shapeUrl a sh:NodeShape;
		sh:or ?unionTypesList .
	?unionTypesListRest rdf:first ?unionTypesListHeadShape ;
		rdf:rest ?unionTypesListTailShape .
} WHERE {
	?type a  ?typeClassUrl .
	VALUES ?typeClassUrl {owl:Class rdfs:Class} .
	OPTIONAL {  
		?type owl:unionOf ?unionTypesList .
		?unionTypesList rdf:rest* ?unionTypesListRest . 
	    ?unionTypesListRest rdf:first ?unionTypesListHead . 
	    ?unionTypesListRest  rdf:rest ?unionTypesListTail .
	  	BIND ( IF ( ?unionTypesListHead != rdf:nil && !isBlank(?unionTypesListHead), URI(CONCAT(STR(?unionTypesListHead),'Shape')), ?unionTypesListHead ) AS ?unionTypesListHeadShape ) 
	  	BIND ( IF ( ?unionTypesListTail != rdf:nil && !isBlank(?unionTypesListTail), URI(CONCAT(STR(?unionTypesListTail),'Shape')), ?unionTypesListTail ) AS ?unionTypesListTailShape )
	}
	FILTER (!isBlank(?type)) .
	BIND ( URI(CONCAT(STR(?type),'Shape')) AS ?shapeUrl) .
}"
Y;0;Comment annotation;owl:Class, rdfs:Class, rdfs:comment;sh:NodeShape, sh:description, rdfs:label;Non-Validating Property Shape Characteristics;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>

CONSTRUCT { 
	?shapeUrl a sh:NodeShape;
 		sh:description ?shapeNodeDescription;
 		rdfs:label ?shapeNodeDescription .
} WHERE {
    ?type a  ?typeClassUrl .
	VALUES ?typeClassUrl {owl:Class rdfs:Class} .
    ?type rdfs:comment ?shapeNodeDescription . 
 	FILTER (!isBlank(?type)) .
 	BIND ( URI(CONCAT(STR(?type),'Shape')) AS ?shapeUrl) .
}"
Y;0;Label annotation;owl:Class, rdfs:Class, rdfs:label;sh:NodeShape,sh:name, rdfs:label;Non-Validating Property Shape Characteristics;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>

CONSTRUCT { 
	?shapeUrl a sh:NodeShape;
		sh:name ?shapeNodeName;
		rdfs:label ?shapeNodeName .
} WHERE {
    ?type a  ?typeClassUrl .
	VALUES ?typeClassUrl {owl:Class rdfs:Class} .
	?type rdfs:label ?shapeNodeName .
 	FILTER (!isBlank(?type)) .
 	BIND ( URI(CONCAT(STR(?type),'Shape')) AS ?shapeUrl) .
}"
Y;0;Complement of a class;owl:Class,rdfs:Class, owl:complementOf, owl:onProperty, owl:Restriction;sh:NodeShape,sh:not, sh:PropertyShape, sh:hasValue, sh:path;REVIEW Logical Constraint  Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

CONSTRUCT {  
 	?shapeUrl a sh:NodeShape ;
		sh:not  ?embeddedNotRestriction .
 	?embeddedNotRestriction a sh:PropertyShape;
		sh:hasValue ?complementaryPropertyRange;
		sh:path ?complementaryProperty .

} WHERE {
 	?type a  ?typeClassUrl . 
	VALUES ?typeClassUrl {owl:Class rdfs:Class} .
	?type owl:complementOf ?owlPropertyRestriction .
	?owlPropertyRestriction a owl:Restriction .
	?owlPropertyRestriction owl:onProperty ?complementaryProperty .
	?owlPropertyRestriction owl:hasValue ?complementaryPropertyRange .
	BIND ( URI(CONCAT(STR(?complementaryProperty),'-EmbeddedNotRestriction')) AS ?embeddedNotRestriction)	
	FILTER (!isBlank(?type)) .
	BIND ( URI(CONCAT(STR(?type),'Shape')) AS ?shapeUrl) .
}"
Y;0;Datatype definition;owl:DatatypeProperty, rdfs:Datatype;sh:PropertyShape, sh:nodeKind, sh:Literal;Value Type Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
	?shapeUrl a sh:PropertyShape;
		sh:nodeKind sh:Literal; 
} WHERE {
	?property a ?propertyType .
	VALUES ?propertyType {owl:DatatypeProperty rdfs:Datatype}
 	FILTER (!isBlank(?property)) .
 	BIND ( URI(CONCAT(STR(?property),'-Shape')) AS ?shapeUrl) .
}"
Y;0;Maximum Exclusive restriction;owl:DatatypeProperty, owl:onDatatype, owl:withRestrictions, xsd:maxExclusive;sh:PropertyShape, sh:maxExclusive;String-based Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  ?shapeUrl a sh:PropertyShape .
  ?shapeUrl sh:maxExclusive ?restrictionMax .
} WHERE {
	?property a ?propertyType .
	VALUES ?propertyType {owl:DatatypeProperty rdfs:Datatype}
 	OPTIONAL { ?property owl:withRestrictions ?restrictionsList .
 		?restrictionsList rdf:rest*/rdf:first ?restrictionElement .
 		OPTIONAL { ?restrictionElement xsd:maxExclusive ?restrictionMax . }
 	}
 	FILTER (!isBlank(?property)) .
 	BIND ( URI(CONCAT(STR(?property),'-Shape')) AS ?shapeUrl) .
 }
"
Y;0;Maximum Inclusive restriction;owl:DatatypeProperty, owl:onDatatype, owl:withRestrictions, xsd:maxInclusive;sh:PropertyShape, sh:maxInclusive;String-based Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  ?shapeUrl a sh:PropertyShape .
  ?shapeUrl sh:maxInclusive ?restrictionMax .
} WHERE {
	?property a ?propertyType .
	VALUES ?propertyType {owl:DatatypeProperty rdfs:Datatype}
 	OPTIONAL { ?property owl:withRestrictions ?restrictionsList .
 		?restrictionsList rdf:rest*/rdf:first ?restrictionElement .
 		OPTIONAL { ?restrictionElement xsd:maxInclusive ?restrictionMax . }
 	}
 	FILTER (!isBlank(?property)) .
 	BIND ( URI(CONCAT(STR(?property),'-Shape')) AS ?shapeUrl) .
 }
"
Y;0;Maximum length restriction;owl:DatatypeProperty, owl:onDatatype, owl:withRestrictions, xsd:maxLength;sh:PropertyShape, sh:maxLength;String-based Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  ?shapeUrl a sh:PropertyShape .
  ?shapeUrl sh:maxLength ?restrictionMax .
} WHERE {
	?property a ?propertyType .
	VALUES ?propertyType {owl:DatatypeProperty rdfs:Datatype}
 	OPTIONAL { ?property owl:withRestrictions ?restrictionsList .
 		?restrictionsList rdf:rest*/rdf:first ?restrictionElement .
 		OPTIONAL { ?restrictionElement xsd:maxLength ?restrictionMax . }
 	}
 	FILTER (!isBlank(?property)) .
 	BIND ( URI(CONCAT(STR(?property),'-Shape')) AS ?shapeUrl) .
 }
"
Y;0;Minimum length restriction;owl:DatatypeProperty, owl:onDatatype, owl:withRestrictions, xsd:minExclusive;sh:PropertyShape, sh:minExclusive;String-based Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  ?shapeUrl a sh:PropertyShape .
  ?shapeUrl sh:minExclusive ?restrictionMin .
} WHERE {
	?property a ?propertyType .
	VALUES ?propertyType {owl:DatatypeProperty rdfs:Datatype}
 	?property owl:withRestrictions ?restrictionsList .
 	?restrictionsList rdf:rest*/rdf:first ?restrictionElement .
 		OPTIONAL { ?restrictionElement xsd:minExclusive ?restrictionMin . }
 	FILTER (!isBlank(?property)) .
 	BIND ( URI(CONCAT(STR(?property),'-Shape')) AS ?shapeUrl) .
 }
"
Y;0;Minimum inclusive restriction;owl:DatatypeProperty, owl:onDatatype, owl:withRestrictions, xsd:minInclusive;sh:PropertyShape, sh:minInclusive;String-based Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  ?shapeUrl a sh:PropertyShape .
  ?shapeUrl sh:minInclusive ?restrictionMin .
} WHERE {
	?property a ?propertyType .
	VALUES ?propertyType {owl:DatatypeProperty rdfs:Datatype}
 	?property owl:withRestrictions ?restrictionsList .
 	?restrictionsList rdf:rest*/rdf:first ?restrictionElement .
 		OPTIONAL { ?restrictionElement xsd:minInclusive ?restrictionMin . }
 	FILTER (!isBlank(?property)) .
 	BIND ( URI(CONCAT(STR(?property),'-Shape')) AS ?shapeUrl) .
 }
"
Y;0;Minumum length restriction;owl:DatatypeProperty, owl:onDatatype, owl:withRestrictions, xsd:minLength;sh:PropertyShape, sh:minLength;String-based Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  ?shapeUrl a sh:PropertyShape .
  ?shapeUrl sh:minLength ?restrictionMin .
} WHERE {
	?property a ?propertyType .
	VALUES ?propertyType {owl:DatatypeProperty rdfs:Datatype}
 	?property owl:withRestrictions ?restrictionsList .
 	?restrictionsList rdf:rest*/rdf:first ?restrictionElement .
 		OPTIONAL { ?restrictionElement xsd:minLength ?restrictionMin . }
 	FILTER (!isBlank(?property)) .
 	BIND ( URI(CONCAT(STR(?property),'-Shape')) AS ?shapeUrl) .
 }
"
Y;0;Minimum length restriction;owl:DatatypeProperty, owl:onDatatype, owl:withRestrictions, xsd:minLength;sh:PropertyShape, sh:maxLength, sh:minLength;String-based Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  ?shapeUrl a sh:PropertyShape .
  ?shapeUrl sh:maxLength ?restrictionLength .
  ?shapeUrl sh:minLength ?restrictionLength .
} WHERE {
	?property a ?propertyType .
	VALUES ?propertyType {owl:DatatypeProperty rdfs:Datatype}
 	?property owl:withRestrictions ?restrictionsList .
 	?restrictionsList rdf:rest*/rdf:first ?restrictionElement .
 		OPTIONAL { ?restrictionElement xsd:length ?restrictionLength . }
 	FILTER (!isBlank(?property)) .
 	BIND ( URI(CONCAT(STR(?property),'-Shape')) AS ?shapeUrl) .
 }
"
Y;0;Range of a datatype;owl:DatatypeProperty, rdfs:Datatype, rdfs:range;sh:PropertyShape, sh:datatype;Value Type Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

CONSTRUCT { ?shapeUrl a sh:PropertyShape ;
				sh:datatype ?typeInRange .
} WHERE {
	?property a ?propertyType .
	VALUES ?propertyType {owl:DatatypeProperty rdfs:Datatype}
	?property rdfs:range ?typeInRange.
 	FILTER (!isBlank(?property)) .
 	BIND ( URI(CONCAT(STR(?property),'-Shape')) AS ?shapeUrl) .
}"
Y;0;Equivalent classes;owl:equivalentProperty, owl:ObjectProperty, owl:DatatypeProperty, rdf:Property, rdfs:Datatype;sh:PropertyShape, sh:equals;Property Pair  Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

CONSTRUCT {  ?shapeUrl a sh:PropertyShape .
			   ?shapeUrl sh:equivalent ?equivalentProperty .
} WHERE {
	?property a ?propertyType .
	VALUES ?propertyType { owl:ObjectProperty owl:DatatypeProperty rdf:Property rdfs:Datatype } .
	MINUS { ?propertyType a owl:InverseFunctionalProperty } .
	MINUS { ?propertyType a owl:FunctionalProperty } .						 
	OPTIONAL { ?property owl:equivalentProperty ?equivalentProperty . }
	BIND ( URI(CONCAT(STR(?property),'-Shape')) AS ?shapeUrl) .
}
"
Y;0;Inverse Functional Property;owl:InverseFunctionalProperty;sh:PropertyShape, sh:inversePath, sh:maxCount;Property Paths and Cardinality Constraint Components;"PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX sh: <http://www.w3.org/ns/shacl#>

CONSTRUCT { 
	?shapeUrl a sh:PropertyShape;
		sh:maxCount 1;
		sh:inversePath ?property .
} WHERE { 
	?property a owl:InverseFunctionalProperty .
	BIND ( URI(CONCAT(STR(?property),'-Shape')) AS ?shapeUrl) .
}"
Y;0;isDefinedBy annotation;owl:ObjectProperty, owl:DatatypeProperty, rdfs:Datatype, rdfs:isDefinedBy;sh:PropertyShape, rdfs:isDefinedBy;Non-Validating Property Shape Characteristics;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>

CONSTRUCT {  ?shapeUrl a sh:PropertyShape;
				rdfs:isDefinedBy ?shapePropertyDefinedBy .
} WHERE { 
		?property a ?propertyType .
		VALUES ?propertyType {owl:ObjectProperty owl:DatatypeProperty rdfs:Datatype } .
		?property rdfs:isDefinedBy ?shapePropertyDefinedBy .
		FILTER (!isBlank(?property)) .
		BIND ( URI(CONCAT(STR(?property),'-Shape')) AS ?shapeUrl) .
}"
Y;0;Label annotation;owl:ObjectProperty, owl:DatatypeProperty, rdfs:Datatype, rdfs:label;sh:PropertyShape, sh:name, rdfs:label;Non-Validating Property Shape Characteristics;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>

CONSTRUCT {  ?shapeUrl a sh:PropertyShape;
				sh:name  ?propertyName;
				rdfs:label  ?propertyName .
} WHERE { 
	?property a ?propertyType .
	VALUES ?propertyType {owl:ObjectProperty owl:DatatypeProperty rdfs:Datatype } .
	OPTIONAL { ?property rdfs:label ?propertyName . }
	FILTER (!isBlank(?property)) .
	BIND ( URI(CONCAT(STR(?property),'-Shape')) AS ?shapeUrl) .
}"
Y;0;seeAlso annotation;owl:ObjectProperty, owl:DatatypeProperty, rdfs:Datatype, rdfs:seeAlso ;sh:PropertyShape, rdfs:seeAlso;Non-Validating Property Shape Characteristics;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>

CONSTRUCT {  ?shapeUrl a sh:PropertyShape;
				rdfs:seeAlso ?shapePropertySeeAlso .
			   
} WHERE { 
	?property a ?propertyType .
	VALUES ?propertyType {owl:ObjectProperty owl:DatatypeProperty rdfs:Datatype } .
	?property rdfs:seeAlso ?shapePropertySeeAlso .
	FILTER (!isBlank(?property)) .
	BIND ( URI(CONCAT(STR(?property),'-Shape')) AS ?shapeUrl) .
}"
Y;0;Pattern restriction;owl:DatatypeProperty, rdfs:Datatype, owl:withRestrictions, xsd:pattern;sh:PropertyShape, sh:pattern;String-based Constraints;"PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  ?shapeUrl a sh:PropertyShape .
  ?shapeUrl sh:pattern  ?restrictionPattern  .
 } WHERE { 
 	?property a ?propertyType .
	VALUES ?propertyType {owl:DatatypeProperty rdfs:Datatype}
 	?property owl:withRestrictions ?restrictionsList .
 	?restrictionsList rdf:rest*/rdf:first ?restrictionElement .
 	OPTIONAL { ?restrictionElement xsd:pattern ?restrictionPattern . }
 	FILTER (!isBlank(?property)) .
 	BIND ( URI(CONCAT(STR(?property),'-Shape')) AS ?shapeUrl) .
}"